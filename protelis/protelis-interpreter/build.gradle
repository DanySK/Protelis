buildscript {
  repositories {
     mavenCentral()
  }
  dependencies {
      if (isJava7Legacy) {
          classpath 'me.tatarka:gradle-retrolambda:3.7.1'
      }
  }
}

if (isJava7Legacy) {
    apply plugin: 'me.tatarka.retrolambda'
    retrolambda {
      javaVersion JavaVersion.VERSION_1_7
      defaultMethods true
      incremental true
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    api("net.sf.trove4j:trove4j:$trove4jVersion")
    api("net.sourceforge.streamsupport:streamsupport:${streamVersion}")
    api("org.eclipse.emf:org.eclipse.emf.ecore:$emfVersion")
    implementation "commons-codec:commons-codec:$commonsCodecVersion"
    implementation "commons-io:commons-io:$commonsIOVersion"
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "org.apache.commons:commons-lang3:$lang3Version"
    implementation "org.danilopianini:javalib:$javalibVersion"
    implementation "org.springframework:spring-core:$springVersion"
    /*
     * Legacy dependencies picking
     */
    if (isJava7Legacy) {
        println("Configuring legacy dependencies")
        api("org.eclipse.xtext:org.eclipse.xtext.common.types:2.10.0")
        api("org.protelis:protelis.parser:${parserVersion}.java7legacy")
        implementation "com.google.code.findbugs:findbugs:$spotBugsLegacy"
    } else {
        api("org.protelis:protelis.parser:$parserVersion")
        api("org.eclipse.xtext:org.eclipse.xtext.common.types:${xtextVersion}")
        implementation "com.github.spotbugs:spotbugs-annotations:$spotBugsVersion"
    }
}

eclipse {
    classpath {
       downloadSources=true
    }
}
